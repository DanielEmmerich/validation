/*?**********************************************

Procedure On-Sight : classical B example

Author : Beno√Æt Lucet (benoit.lucet@systerel.fr)

*************************************************

Train information : location and speed

**********************************************?*/

REFINEMENT
	os_train_info_r

REFINES
	os_train_info

SEES
	os_typ,
	os_in,
	os_consist

ABSTRACT_VARIABLES
	train_position_prev,
	posoff_max_safe_front,
	posoff_max_safe_front_prev,
	posoff_min_safe_front,
	posoff_min_safe_front_prev,
	train_speed

INITIALISATION
	train_position_prev := k_train_position_def ||
	posoff_max_safe_front := 0 ||
	posoff_max_safe_front_prev := k_posoff_max_safe_front_def ||
	posoff_min_safe_front := 0 ||
	posoff_min_safe_front_prev := k_posoff_min_safe_front_def ||
	train_speed := 0

OPERATIONS
	
	elaborate_train_location =
	IF train_position /= train_position_prev THEN
		posoff_max_safe_front := dist_antenna_max_safe_front ||
		posoff_min_safe_front := dist_antenna_min_safe_front
	ELSE
		posoff_max_safe_front,
		posoff_min_safe_front
		:(
			posoff_max_safe_front : NAT &
			posoff_max_safe_front = distance_odo + posoff_max_safe_front_prev &

			posoff_min_safe_front : NAT &
			posoff_min_safe_front = distance_odo + posoff_min_safe_front_prev)
	END
	;

	elaborate_train_speed =
	BEGIN
		train_speed
		:(
			train_speed : NAT &
			train_speed = (distance_odo * k_s_to_ms) / k_period_appli)
	END
	;

	copy_train_info_prec =
	BEGIN
		train_position_prev := train_position ||
		posoff_max_safe_front_prev := posoff_max_safe_front ||
		posoff_min_safe_front_prev := posoff_min_safe_front
	END

END
